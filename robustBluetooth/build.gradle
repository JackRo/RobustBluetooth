apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 20200702
        versionName "1.0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'io.reactivex.rxjava2:rxjava:2.2.19'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
}

/** 以下开始是将Android Library上传到JCenter的相关配置 **/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 项目主页
def siteUrl = 'https://github.com/JackRo/RobustBluetooth'
// 项目的版本控制地址
def gitUrl = 'https://github.com/JackRo/RobustBluetooth.git'

// 发布到组织名称名字，必须填写
group = "easy.robust.bluetooth"
// 发布到JCenter上的包名，必须填写
def libName = "robustBluetooth"
// 版本号，下次更新是只需要更改版本号即可
version = "1.0.4"
/**  上面配置后上传至JCenter后的编译路径是这样的: compile 'easy.robust.bluetooth:robustBluetooth:1.0.0'  **/

// 生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    // options.links "https://github.com/JackRo/RobustBluetooth"
    failOnError false
}

// 文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// 拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

// 上传到JCenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'Robust Android Classic Bluetooth connect and transfer data to Bluetooth device'
                url siteUrl
                licenses {
                    license {
                        name 'Robust Android Classic Bluetooth connect and transfer data to Bluetooth device'
                        url 'https://github.com/JackRo/RobustBluetooth'
                    }
                }
                developers {
                    developer {
                        id 'JackRo'
                        name 'zhuochangjing'
                        email 'zhuochangjing07@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

// 上传到JCenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    // 读取 local.properties 文件里面的 bintray.user
    user = properties.getProperty("bintray.user")
    // 读取 local.properties 文件里面的 bintray.apikey
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        // JCenter上的组织名，必须填写
        userOrg = "robust-bluetooth"
        // JCenter上的仓库名，必须填写
        repo = "robustBluetooth"
        // 发布到JCenter上的包名，必须填写
        name = libName
        // 项目描述
        desc = 'Robust Android Classic Bluetooth connect and transfer data to Bluetooth device'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}